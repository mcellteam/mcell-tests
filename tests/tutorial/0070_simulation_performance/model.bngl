
## 0030-2)
## 0040-2)
begin molecule types
  a
  b
  t1
  c
  # 0060-5)
  # We will need two more molecule types 'd' and 't2'
  d
  t2
end molecule types 


## 0030-3)
## 0040-3)
begin parameters
  # unit of diffusion constants is cm^2/s
  MCELL_DIFFUSION_CONSTANT_3D_a 1e-6
  MCELL_DIFFUSION_CONSTANT_3D_b 1e-6
  MCELL_DIFFUSION_CONSTANT_3D_c 1e-6
  MCELL_DIFFUSION_CONSTANT_2D_t1 1e-7
  
  # 0060-6)
  # Provide diffusion constants for 'd' and 't2'
  MCELL_DIFFUSION_CONSTANT_3D_d 1e-6
  MCELL_DIFFUSION_CONSTANT_2D_t2 1e-8
    
  # unit is the number of molecules
  num_a_to_release 1000
  num_b_to_release 1000
  num_t1_to_release 700

  # 0060-7)
  # We will be releasing 700 't2' molecules 
  num_t2_to_release 700
  
  # unit of these bimolecular reaction rate constants is 1/M*1/s
  rate_a_b_to_c 1e9 
  rate_a_out_t1_to_a_in_t1 3e8
  
  # 0060-8)
  # Let's also prepare constants that represent rates for new reactions.
  # These reactions will be explained later.
  rate_c_in_t1_to_c_out_t1 3.3e8
  rate_c_out_t2_to_d_in_t2 3e8
   
end parameters  


## 0030-4)
## 0040-4)
begin compartments
  Cell 3 1
  Organelle_1_surface 2 0 Cell
  Organelle_1 3 1 Organelle_1_surface

  # 0060-9)
  # We need one more 3D and one 2D compartment for the new 
  # organelle 2. The Cell compartment is a shared parent.  
  Organelle_2_surface 2 0 Cell
  Organelle_2 3 1 Organelle_2_surface
end compartments  


## 0030-5)
## 0040-5)
begin seed species
  a@Cell num_a_to_release
  t1@Organelle_1_surface num_t1_to_release
  b@Organelle_1 num_b_to_release
  
  # 0060-10)
  # Release transporter 't2' on the second organelle's surface.  
  t2@Organelle_2_surface num_t2_to_release
  
end seed species


## 0040-6)
begin reaction rules

  ## 0040-7) 
  a + b -> c  rate_a_b_to_c  

  ## 0040-8)
  a@Cell + t1@Organelle_1_surface -> a@Organelle_1 + t1@Organelle_1_surface  rate_a_out_t1_to_a_in_t1
  
  # 0060-11)
  # Add reaction rules that transport 'c' out from Organelle_1 using transporter 't1',
  # and also transport 'c' into Organelle_2 using transporter 't2' while changing it 
  # to 'd'
  c@Organelle_1 + t1@Organelle_1_surface -> c@Cell + t1@Organelle_1_surface  rate_c_in_t1_to_c_out_t1
  
  c@Cell + t2@Organelle_2_surface -> d@Organelle_2 + t2@Organelle_2_surface  rate_c_out_t2_to_d_in_t2
    
end reaction rules


## 0050-2)
begin observables
  Molecules a a
  Molecules a_in_cell a@Cell
  Molecules a_in_organelle_1 a@Organelle_1
  Molecules b b
  Molecules c c
  Molecules t1 t1
  
  # 0060-12)
  # Let's also count 'c' in diverse compartments and the total number
  # of molecules 'd'.
  Molecules c_in_organelle_1 c@Organelle_1
  Molecules c_in_cell c@Cell
  Molecules d d
   
end observables  
  